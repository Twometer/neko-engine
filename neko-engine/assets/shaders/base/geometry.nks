#begin shared
    vec3 fragmentPosition;
    vec3 fragmentColor;
    vec3 fragmentNormal;
    vec2 fragmentTexCoords;
#end shared

#begin vertex
    layout(location = 0) in vec3 vertexPosition;
    layout(location = 1) in vec3 vertexColor;
    layout(location = 2) in vec3 vertexNormal;
    layout(location = 3) in vec2 vertexTexCoords;

    uniform mat4 viewMatrix;
    uniform mat4 projectionMatrix;
    uniform mat4 modelMatrix;

    void main() {
        vec4 worldSpacePos = modelMatrix * vec4(vertexPosition, 1.0f);
        gl_Position = projectionMatrix * viewMatrix * worldSpacePos;

        mat3 normalMatrix = mat3(transpose(inverse(modelMatrix)));

        fragmentPosition = worldSpacePos.xyz;
        fragmentColor = vertexColor;
        fragmentNormal = normalize(normalMatrix * vertexNormal);
        fragmentTexCoords = vertexTexCoords;
    }
#end vertex

#begin fragment
    layout(location = 0) out vec4 gPosition;
    layout(location = 1) out vec4 gNormal;
    layout(location = 2) out vec4 gAlbedo;

    uniform float specular;
    uniform float shininess;

    uniform sampler2D textureSampler;

    void main() {
        vec4 textureColor = texture(textureSampler, fragmentTexCoords);
        if (textureColor.a <= 0.15)
            discard;

        gPosition = vec4(fragmentPosition, 1.0f);
        gNormal = vec4(fragmentNormal, specular);       // Encode as NormalSpecular
        gAlbedo = vec4(textureColor.rgb, shininess);    // Encode as AlbedoShininess
    }
#end fragment