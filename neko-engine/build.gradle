import org.gradle.internal.os.OperatingSystem

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.30'
}

group 'de.twometer'
version '2.0.0-alpha'

/* Version configuration */
project.ext.lwjglVersion = "3.2.3"
project.ext.imguiVersion = "1.84.1.0"

switch (OperatingSystem.current()) {
    case OperatingSystem.LINUX:
        project.ext.natives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.natives = "natives-macos"
        break
    case OperatingSystem.WINDOWS:
        project.ext.natives = "natives-windows"
        break
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    api platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    api "org.lwjgl:lwjgl"
    api "org.lwjgl:lwjgl-assimp"
    api "org.lwjgl:lwjgl-glfw"
    api "org.lwjgl:lwjgl-meow"
    api "org.lwjgl:lwjgl-openal"
    api "org.lwjgl:lwjgl-opengl"
    api "org.lwjgl:lwjgl-stb"
    api "org.lwjgl:lwjgl-tinyfd"

    api "org.joml:joml:1.10.0"
    api 'org.greenrobot:eventbus:3.2.0'
    api 'io.github.microutils:kotlin-logging:1.12.0'
    api 'org.slf4j:slf4j-api:1.7.30'
    api 'com.labymedia:ultralight-java-base:0.3.6'
    api 'com.labymedia:ultralight-java-databind:0.3.6'
    api "io.github.spair:imgui-java-binding:$imguiVersion"
    api "io.github.spair:imgui-java-lwjgl3:$imguiVersion"

    runtimeOnly "org.lwjgl:lwjgl::$natives"
    runtimeOnly "org.lwjgl:lwjgl-assimp::$natives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$natives"
    runtimeOnly "org.lwjgl:lwjgl-meow::$natives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$natives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$natives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$natives"
    runtimeOnly "org.lwjgl:lwjgl-tinyfd::$natives"
    runtimeOnly "io.github.spair:imgui-java-$natives:$imguiVersion"
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}